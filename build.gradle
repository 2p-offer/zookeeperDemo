buildscript {
    project.ext {
        if (!project.hasProperty("nexusUser")) {
            nexusUser = 'deployment'
        }
        if (!project.hasProperty("nexusPass")) {
            nexusPass = 'deployment123'
        }
        nexusRoot = 'http://repo.feinno.com/nexus/content'
        privateRepo = { return { url "${nexusRoot}/groups/public/" } }
        uploadRepo = { project ->
            return {
                url "$nexusRoot/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
                credentials { username nexusUser; password nexusPass }
            }
        }

        repoClosure = {
            maven privateRepo()
            mavenCentral()
            mavenLocal()
        }
    }
    repositories repoClosure
}

allprojects { project ->

    project.repositories rootProject.repoClosure

    task wrapper(type: Wrapper) {
        gradleVersion = '2.12'
    }

    group = 'org.helium'
    version = '3.0.0-SNAPSHOT'
}

def mavenPublishIgnoreList = ['he-server', 'ucloud-sample']

subprojects { project ->

    apply plugin: 'java'
//    apply plugin: 'osgi'
    apply plugin: 'maven-publish'

    tasks.withType(JavaCompile) {
        sourceCompatibility = 1.8
        options.encoding = "UTF-8"
    }

    tasks.withType(Test) {
        defaultCharacterEncoding = "UTF-8"
    }

    dependencies {
        testCompile "junit:junit:'4.2'"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    if (!(project.name in mavenPublishIgnoreList)) {
        publishing {
            repositories {
                maven uploadRepo(project)
            }
            publications {
                maven(MavenPublication) {
                    from components.java
                    artifact sourceJar
                }
            }
        }
    }
}
